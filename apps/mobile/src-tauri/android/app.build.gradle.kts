import java.util.Properties

plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    id("rust")
}

val tauriProperties = Properties().apply {
    val propFile = file("tauri.properties")
    if (propFile.exists()) propFile.inputStream().use { load(it) }
}
val pluginCatalogUrl: String = providers.gradleProperty("viewitPluginCatalogUrl")
    .orElse(providers.environmentVariable("VIEWIT_PLUGIN_CATALOG_URL"))
    .orElse("")
    .get()
val viewitAppProfile: String = providers.gradleProperty("viewitAppProfile")
    .orElse(providers.environmentVariable("VIEWIT_APP_PROFILE"))
    .orElse("development")
    .get()
val supportedViewitProfiles = setOf("development", "device-test", "production")
require(viewitAppProfile in supportedViewitProfiles) {
    "VIEWIT_APP_PROFILE must be one of ${supportedViewitProfiles.joinToString()}; got $viewitAppProfile"
}
val mediaWorkerEnabled: Boolean = providers.gradleProperty("viewitMediaWorkerEnabled")
    .orElse(providers.environmentVariable("VIEWIT_MEDIA_WORKER_ENABLED"))
    .orElse("0")
    .get() == "1"
require(!mediaWorkerEnabled || viewitAppProfile != "production") {
    "The media worker prototype cannot be enabled for production before provider conformance approval"
}

android {
    compileSdk = 36
    namespace = "ai.viewit.app"
    defaultConfig {
        manifestPlaceholders["usesCleartextTraffic"] = "false"
        applicationId = "ai.viewit.app"
        minSdk = 24
        targetSdk = 36
        versionCode = tauriProperties.getProperty("tauri.android.versionCode", "1").toInt()
        versionName = tauriProperties.getProperty("tauri.android.versionName", "1.0")
        buildConfigField("String", "VIEWIT_PLUGIN_CATALOG_URL", "\"${pluginCatalogUrl.replace("\\", "\\\\").replace("\"", "\\\"")}\"")
        buildConfigField("String", "VIEWIT_APP_PROFILE", "\"$viewitAppProfile\"")
        buildConfigField("boolean", "VIEWIT_MEDIA_WORKER_ENABLED", mediaWorkerEnabled.toString())
    }
    buildTypes {
        getByName("debug") {
            manifestPlaceholders["usesCleartextTraffic"] = "true"
            isDebuggable = true
            isJniDebuggable = true
            isMinifyEnabled = false
        }
        getByName("release") {
            manifestPlaceholders["usesCleartextTraffic"] = "false"
            isMinifyEnabled = true
            proguardFiles(
                *fileTree(".") { include("**/*.pro") }
                    .plus(getDefaultProguardFile("proguard-android-optimize.txt"))
                    .toList().toTypedArray()
            )
        }
    }
    kotlinOptions { jvmTarget = "1.8" }
    buildFeatures { buildConfig = true }
    packaging { jniLibs.useLegacyPackaging = true }
}

rust { rootDirRel = "../../../" }

dependencies {
    implementation("androidx.webkit:webkit:1.14.0")
    implementation("androidx.appcompat:appcompat:1.7.1")
    implementation("androidx.activity:activity-ktx:1.10.1")
    implementation("com.google.android.material:material:1.12.0")
    implementation("androidx.lifecycle:lifecycle-process:2.10.0")
    implementation("androidx.media3:media3-exoplayer:1.5.1")
    implementation("androidx.media3:media3-ui:1.5.1")
    implementation("androidx.media3:media3-exoplayer-rtsp:1.5.1")
    implementation("androidx.media3:media3-datasource-okhttp:1.5.1")
    implementation("net.i2p.crypto:eddsa:0.3.0")
    testImplementation("junit:junit:4.13.2")
    androidTestImplementation("androidx.test.ext:junit:1.1.4")
    androidTestImplementation("androidx.test.espresso:espresso-core:3.5.0")
}

apply(from = "tauri.build.gradle.kts")
